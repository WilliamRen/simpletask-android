buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}

apply plugin: 'android'

android {
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    repositories {
        mavenCentral()
    }

    buildToolsVersion "19.0.0"
    compileSdkVersion 19

    defaultConfig {
        versionName '4.1.3'
        versionCode 1069
    }


    productFlavors {
        free {
            packageName 'nl.mpcjanssen.todotxtholo'
            buildConfigField "boolean", "CLOUDLESS", "false"
        }
        // Special flavor for testing as this is uninstalled after
        // completion of the test run
        unit {
            packageName 'nl.mpcjanssen.simpletask.test'
            buildConfigField "boolean", "CLOUDLESS", "true"
        }
        cloudless {
            packageName 'nl.mpcjanssen.simpletask'
            buildConfigField "boolean", "CLOUDLESS", "true"
        }
    }

    dependencies {
        freeCompile fileTree(dir: 'libs', include: '*.jar')
        compile 'com.android.support:support-v4:19.0.0'
        compile 'joda-time:joda-time:2.3'
	compile 'org.commonjava.googlecode.markdown4j:markdown4j:2.2-cj-1.0'
    }

    signingConfigs {
        release {
            keyAlias = "mpcjanssen.nl"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    check.dependsOn 'connectedInstrumentTestUnitDebug'
}

// Only sign if we have keystore properties in the gradle.properties
if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
